buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'net.kyori.blossom' version '1.2.0'
}


apply plugin: 'net.minecraftforge.gradle'

version = modVersion
group = modGroup

minecraft {

    mappings channel: project.mcpChannel, version: project.mcpVersion
    accessTransformer = file('src/main/resources/META-INF/placeholders_at.cfg')

}

repositories {
    mavenCentral()
    maven {
        name = 'sponge-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://jitpack.io'
    }
    ivy {
        name = 'Pixelmon Reforged'
        url "https://download.nodecdn.net/containers/reforged/server/release"
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact "[revision]/[artifact].[ext]"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + forgeVersion
    compileOnly 'org.spongepowered:spongeapi:' + spongeVersion
    compileOnly 'com.github.rojo8399:PlaceholderAPI:4.5.1'
    compileOnly 'pixelmon:Pixelmon-1.12.2-' + pixelmonVersion + '-server:' + pixelmonVersion
}

blossom {
    replaceToken '@version@', modVersion, 'src/main/java/com/github/happyzleaf/pixelmonplaceholders/PixelmonPlaceholders.java'
}


processResources {

    filesMatching("mcmod.info") {
        expand(
                'version': project.modVersion,
                'mcversion': project.mcVersion
        )
    }

}
